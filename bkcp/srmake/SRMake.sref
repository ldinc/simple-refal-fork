//FROM Library
$EXTERN WriteLine, System;

//FROM LibraryEx
$EXTERN ArgList, Map, Fetch;

//FROM FileScanner
$EXTERN CreateFileList;

//FROM ParseCmdLine
$EXTERN ParseCommandLine, NoCompile, CompileCommand, CmdLineError;

$FORWARD Main;

$ENTRY Go { = <Main <ArgList>>; }

$FORWARD MakeProject, Get-s-option;

Main {
  (e.Program) =
    <WriteLine 'Usage:'>
    <WriteLine
      '  srmake [-s path\\to\\srefc.exe] -c c++compiler {-d search-dir} MainFileName[.sref]'
    >;

  (e.Program) e.Arguments =
    <MakeProject
      <Fetch
        <Get-s-option e.Arguments>
        {
          (e.SrefC) e.RestOfArguments =
            (e.SrefC) <ParseCommandLine e.RestOfArguments>;
        }
      >
    >;
}

/*
  Да, костыль, но усложнение потребует большего объёма кода.
  Костыль имеет недочёт: в командной строке может быть
    srmake -c -s srefc g++ MainFile.sref
  и эта строка корректно обработается.
*/
Get-s-option {
  e.Arguments-B ('--') e.Arguments-E =
    <Get-s-option e.Arguments-B> ('--') e.Arguments-E;

  e.Arguments-B ('-s') (e.SrefC) e.Arguments-E =
    (e.SrefC) e.Arguments-B e.Arguments-E;

  e.Arguments = ('srefc') e.Arguments;
}

$FORWARD Make;

MakeProject {
  (e.SrefC) (CmdLineError e.Message) =
    <WriteLine 'COMMAND LINE ERROR: ' e.Message>;

  (e.SrefC) (NoCompile) (e.Folders) (e.File) =
    <WriteLine
      'COMMAND LINE ERROR: Compiler not selected'
    >;

  (e.SrefC) (CompileCommand e.Command) (e.Folders) (e.File) =
    <Make
      (e.SrefC) (e.Command) (e.Folders) <CreateFileList (e.Folders) e.File>
    >;

  (e.SrefC) t.Compiler (e.Folders) e.ManyFiles =
    <WriteLine
      'COMMAND LINE ERROR: many files selected'
    >;
}

//FROM FindFile
$EXTERN Source, Output, NotFound;

$FORWARD PrintNotFound, PrintDirectory, PrintUnit;

Make {
  (e.SrefC) (e.CompilerCommand) (e.Directories)
  e.Units-B (NotFound e.UnitName) e.Units-E =
    <Map PrintNotFound (NotFound e.UnitName) e.Units-E>;

  (e.SrefC) (e.CompilerCommand) (e.Directories) e.Units =
    <System
      e.SrefC ' -c "' e.CompilerCommand '" '
      <Map PrintDirectory e.Directories>
      <Map PrintUnit e.Units>
    >;
}

PrintNotFound {
  (NotFound e.UnitName) =
    <WriteLine
      'COMMAND LINE ERROR: Unit ' e.UnitName ' not found'
    >;

  (Output e.Output) = ;
  (Source (e.Source) e.Output) = ;
}

PrintDirectory {
  (e.Folder) = '-d "' e.Folder '" ';
}

PrintUnit {
  (Output e.Output) = '"' e.Output '" ';
  (Source (e.Source) e.Output) = '"' e.Source '" ';
}
